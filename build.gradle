apply plugin: 'java'
apply plugin: 'idea'
// Apply the application plugin to add support for building an application
apply plugin: 'application'
apply from: 'gradle/service-test.gradle'

// In this section you declare where to find the dependencies of your project
repositories {

    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/libs-milestone' }

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

sourceSets {
    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functional-test/java')
        }
        resources.srcDir file('src/functional-test/resources')
    }
}

configurations {
    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime
}

task functionalTest(type: Test) {
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    compile 'com.google.guava:guava:23.0',
            'org.springframework.boot:spring-boot-starter-web:2.0.0.M3',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    testCompile 'junit:junit:4.12',
            'org.assertj:assertj-core:3.8.0',
            'org.springframework.boot:spring-boot-starter-test:2.0.0.M3'

}

// Define the main class for the application
mainClassName = 'uk.tw.energy.App'

